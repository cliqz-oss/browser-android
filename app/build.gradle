apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'
apply from: '../scripts/CliqzConfig.gradle'
apply from: 'cliqz.gradle'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"
    defaultConfig {
        targetSdkVersion 25
        minSdkVersion 21
        versionCode 35
        versionName "1.1.2"

        multiDexEnabled true

        buildConfigField 'String', 'LIGHTNING_VERSION_NAME', '"4.2.3.1"'
        buildConfigField "boolean", "FULL_VERSION", "true"
        buildConfigField "int", "MINIMUM_WEBVIEW_VERSION", "55"

        applicationId "com.cliqz.browser"
        testInstrumentationRunner "com.cliqz.browser.test.CustomTestRunner"
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFile getDefaultProguardFile('proguard-android.txt')
            shrinkResources false
            proguardFiles 'proguard-project.txt'
            buildConfigField 'String', 'TELEMETRY_CHANNEL', '"MA02"'
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
        }

        release {
            minifyEnabled false
            proguardFile getDefaultProguardFile('proguard-android.txt')
            shrinkResources false
            proguardFiles 'proguard-project.txt'
            buildConfigField 'String', 'TELEMETRY_CHANNEL', '"MA00"'
        }
    }
    packagingOptions {
        exclude '.readme'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
    }
    sourceSets {
        lookback {
            java.srcDirs += [
                    'src/standard/java',
            ]
            res.srcDirs += [
                    'src/standard/res',
            ]
        }
    }

    flavorDimensions "api", "abi"

    productFlavors {
        standard {
            dimension "api"
            versionCode 5
        }
        xwalk {
            dimension "api"
            minSdkVersion 16
            versionCode 1
        }
        lookback {
            dimension "api"
            targetSdkVersion 21
            versionCode 9
            applicationId "com.cliqz.browser.ut"
        }
        x86 {
            dimension "abi"
            ndk {
                abiFilter "x86"
            }
            versionCode 5
        }
        arm {
            dimension "abi"
            ndk {
                abiFilter "armeabi-v7a"
            }
            versionCode 3
        }
        fat {
            dimension "abi"
            versionCode 1
        }
    }
}

dexcount {
    includeClasses = false
    includeFieldCount = false
    printAsTree = true
    orderByMethodCount = true
    verbose = false
}

repositories {
    flatDir {
        dirs 'aars'
    }
}

configurations.all {
  resolutionStrategy {
    force 'com.android.support:support-v4:25.0.0'
    force 'com.android.support:support-annotations:25.0.0'
  }
}

dependencies {
    // support libraries
    compile 'com.android.support:appcompat-v7:25.0.0'
    compile 'com.android.support:design:25.0.0'
    xwalkCompile 'com.android.support:multidex:1.0.1'
    // Gson
    compile 'com.google.code.gson:gson:2.6.1'
    // html parsing fo reading mode
    compile 'org.jsoup:jsoup:1.8.3'
    // event bus
    compile 'com.squareup:otto:1.3.8'
    // dependency injection
    compile 'com.google.dagger:dagger:2.0.1'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.0.1'
    provided 'javax.annotation:jsr250-api:1.0'
    // view binding
    compile 'com.jakewharton:butterknife:7.0.1'
    // Anti-tracking
    standardCompile 'com.github.cliqz-oss:jsengine:ef76b5f@aar', {transitive=true}
    lookbackCompile 'com.github.cliqz-oss:jsengine:ef76b5f@aar', {transitive=true}

    // Utils libraries
    compile project(':utils')
    compile project(':android_utils')
    // On-boarding
    compile 'com.github.deano2390:MaterialShowcaseView:1.1.0@aar'
    // memory leak analysis
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    androidTestCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    // Google Play Services
    compile "com.google.android.gms:play-services-gcm:9.4.0"

    // xwalk webview
    xwalkCompile 'org.xwalk:xwalk_core_library:20.50.533.12'
    
    // AWS SNS SDK
    compile 'com.amazonaws:aws-android-sdk-sns:2.+'
    compile 'com.amazonaws:aws-android-sdk-cognito:2.+'

    //Facebook SDK
    compile 'com.facebook.android:facebook-android-sdk:4.+'

    // Loopback.io
    lookbackCompile(name:'lookback-android-sdk-0.9.13', ext:'aar')
    lookbackCompile 'com.squareup.okhttp:okhttp:2.6.0'
    lookbackCompile 'com.squareup.retrofit:retrofit:1.9.0'

    // MiniBloomFilter
    compile 'com.github.cliqz-oss:minibloomfilter:1.0'

    // Testing-only dependencies
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.eclipse.jetty:jetty-server:9.2.17.v20160517'
    // Forcing version
    androidTestCompile 'com.android.support:support-annotations:25.0.0'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-web:2.2.2'
    androidTestCompile 'org.eclipse.jetty:jetty-server:9.2.17.v20160517'
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2.2'

}


preBuild << {
    android.applicationVariants.each { variant ->
        def apiVersion = variant.productFlavors.get(0).versionCode
        def abiVersion = variant.productFlavors.get(1).versionCode
        // set the composite code
        variant.mergedFlavor.versionCode = android.defaultConfig.versionCode * 100 + apiVersion * 10 + abiVersion
    }
}