buildscript {
    ext.kotlin_version = '1.3.31'
    repositories {
        jcenter()
        google()
        maven {
            url 'https://jitpack.io'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.1'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.6'
        classpath 'com.google.gms:google-services:4.2.0'
        classpath 'com.github.cliqz-oss:minibloomfilter:1.0'
        classpath 'com.github.triplet.gradle:play-publisher:1.2.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

ext {
    compileSdkVersion = 28
    targetSdkVersion = 28
    minSdkVersion = 21
    buildToolsVersion = "28.0.3"
    supportLibVersion = "28.0.0"
    reactNativeVersion = "0.59.8"
    butterKnifeVersion = "10.1.0"
}

subprojects { subproject ->
    afterEvaluate{
        if((subproject.plugins.hasPlugin('android') || subproject.plugins.hasPlugin('android-library'))) {
            android {
                compileSdkVersion rootProject.ext.compileSdkVersion
                buildToolsVersion rootProject.ext.buildToolsVersion
                defaultConfig {
                    minSdkVersion rootProject.ext.minSdkVersion
                    targetSdkVersion rootProject.ext.targetSdkVersion
                }
            }
        }
    }
}

allprojects {
    buildscript {
        repositories {
            google()
        }
    }
    repositories {
        jcenter()
        google()
        maven {
            url 'https://jitpack.io'
        }
        maven {
            url  "http://dl.bintray.com/cliqz/cliqz-oss"
        }
        maven {
            url "${rootProject.projectDir}/node_modules/react-native/android"
        }
        maven {
            // Local Maven repo containing AARs with JSC library built for Android
            url "${rootProject.projectDir}/node_modules/jsc-android/dist"
        }
    }

    configurations.all {
        resolutionStrategy {
            force "com.facebook.react:react-native:${reactNativeVersion}"
        }
    }
}
